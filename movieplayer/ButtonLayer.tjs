// ButtonLayer.tjs - button layer
// Copyright (C)2001-2004, W.Dee ãƒ» free to modify and distribute

/*
A layer that acts as a button.
The image when pressed, when the mouse cursor is on the layer
and the normal image can be changed respectively

Format  new ButtonLayer(<window>, <parent>)

<window> : the window in which to create this layer
<parent> : parent layer

*/


class ButtonLayer extends Layer
{
	var Butt_imageLoaded = false; // image loaded
	var Butt_mouseOn = false; // is there a mouse on the layer
	var Butt_mouseDown = false; // is the mouse button down
	var Butt_color = clNone;
	var Butt_caption = ''; // button caption
	var Butt_captionColor = 0x000000; // caption color
	var Butt_keyPressed = false;
	var Butt_showFocusImage = false;
	var Butt_onClickHandler = null;

	function ButtonLayer(win, par)
	{
		super.Layer(win, par);

		if(typeof win.cursorPointed !== "undefined")
			cursor = win.cursorPointed;

		hitType = htMask;
		hitThreshold = 0;
		focusable = true; // can get focus
	}

	function finalize()
	{
		super.finalize(...);
	}

	function assign(src)
	{
		// copy src information to this button layer
		assignImages(src, true);
		Butt_imageLoaded = src.Butt_imageLoaded;
		Butt_color = src.Butt_color;
		Butt_caption = src.Butt_caption;
		Butt_captionColor = src.Butt_captionColor;
		hitThreshold = src.hitThreshold;
		update();
	}

	function drawState(s)
	{
		// draw the image corresponding to state s
		// s :  0 : normal state
		//      1 : button pressed
		//      2 : state where the mouse cursor is over the button
		//     (3): if it has focus
		if(!enabled)
		{
			s = 0; // disabled state
		}

		if(Butt_imageLoaded)
		{
			// button image is loaded
			// TODO: keyboard focus
			imageLeft = -s * width;
		}
		else
		{
			if(Butt_keyPressed) s = 1; // pressed

			// clear drawing frame and 
			// caption
			face = dfAlpha;
			colorRect(0, 0, width, height, 0, -255);
			// paint the base
			if(Butt_color != clNone)
				colorRect(0, 0, width, height, Butt_color, 128);
			// get font size
			var tw, th;
			tw = font.getTextWidth(Butt_caption);
			th = font.getTextHeight(Butt_caption);
			if(s == 0 || s == 2)
			{
				// normal or mouse is over the button
				colorRect(0, 0, width, 1, 0xffffff, 128);
				colorRect(0, 1, 1, height-2, 0xffffff, 128);
				colorRect(width-1, 1, 1, height-1, 0x000000, 128);
				colorRect(1, height-1, width-2, 1, 0x000000, 128);
				drawText((width-tw)>>1, (height-th)>>1, 
					Butt_caption, Butt_captionColor, nodeEnabled?255:128);
			}
			else
			{
				// button is being pressed
				colorRect(0, 0, width, 1, 0x000000, 128);
				colorRect(0, 1, 1, height-2, 0x000000, 128);
				colorRect(width-1, 1, 1, height-1, 0xffffff, 128);
				colorRect(1, height-1, width-2, 1, 0xffffff, 128);
				drawText(((width-tw)>>1) +1, ((height-th)>>1) +1, 
					Butt_caption, Butt_captionColor, nodeEnabled?255:128);
			}

			if(s != 0)
				colorRect(2, 2, width-4, height-4, clHighlight, 64); // highlight

			if(focused)
			{
				// highlight because it has focus
				colorRect(2, 2, width-4, 1, clHighlight, 128);
				colorRect(2, 3, 1, height-5, clHighlight, 128);
				colorRect(3, height-3, width-5, 1, clHighlight, 128);
				colorRect(width-3, 3, 1, height-6, clHighlight, 128);
			}
		}
	}

	function loadImages(storage, key)
	{
		// loading Images
		super.loadImages(storage, key);
		super.width = imageWidth \ (Butt_showFocusImage ? 4 : 3);
		super.height = imageHeight;
		callOnPaint = true;
		Butt_imageLoaded = true;
	}

	function discardImage()
	{
		// Discard the image and make it act as a text button layer
		Butt_imageLoaded = false;
		imageLeft = imageTop = 0;
		update();
	}

	function onMouseDown()
	{
		// onMouseDown event handler 
		Butt_mouseDown = true;
		focus();
		update();
		super.onMouseDown(...);
	}

	function onMouseUp()
	{
		// onMouseUp event handler
		Butt_mouseDown = false;
		update();
		super.onMouseUp(...);
	}

	function onClick()
	{
		// onClick event handler
		super.onClick(...);
		if(Butt_onClickHandler !== null)
			Butt_onClickHandler();
	}

	function draw()
	{
		// drawing according to the current state 
		if(Butt_mouseDown) drawState(1);
		else if(Butt_mouseOn) drawState(2);
		else if(Butt_showFocusImage && focused) drawState(3);
		else drawState(0);
	}

	function onPaint()
	{
		// called just before drawing 
		super.onPaint(...);
		draw();
	}

	function onMouseEnter()
	{
		// the mouse is inside the layer area.
		update();
		Butt_mouseOn = true;
		super.onMouseEnter(...);
	}

	function onMouseLeave()
	{
		// the mouse is outside the layer area.
		update();
		Butt_mouseOn = false;
		Butt_mouseDown = false;
		super.onMouseLeave(...);
	}

	function onNodeDisabled()
	{
		// layer node is disabled
		super.onNodeDisabled(...);
		Butt_mouseDown = false;
		update();
	}

	function onNodeEnabled()
	{
		// layer node is enabled
		super.onNodeEnabled(...);
		update();
	}

	function onFocus()
	{
		// gained focus
		super.onFocus(...);
		update();
	}

	function onBlur()
	{
		// lost focus
		super.onBlur(...);
		Butt_mouseDown = false;
		update();
	}

	function onKeyDown(key, shift, process)
	{
		// key is pressed 
		if(process)
		{
			if(key == VK_RETURN || key == VK_SPACE)
			{
				// Enter or Space key is pressed
				Butt_keyPressed = true;
				update();
				super.onKeyDown(key, shift, false); // pass false to process as it has been processed
			}
			else
			{
				super.onKeyDown(...);
			}
		}
		else
		{
			// If process is false, no processing is performed.
			super.onKeyDown(...);
		}
	}

	function onKeyUp(key, shift, process)
	{
		// key is released
		if(process)
		{
			if(key == VK_RETURN || key == VK_SPACE)
			{
				// Enter or Space key is released
				var flag = Butt_keyPressed;
				Butt_keyPressed = false;
				update();
				super.onKeyUp(key, shift, false);
				if(flag) onClick(width \ 2, height \ 2); // click event
			}
			else
			{
				super.onKeyUp(...);
			}
		}
		else
		{
			super.onKeyUp(...);
		}
	}

	function onKeyPress(key, shift)
	{
		super.onKeyPress(...);
	}


	property width
	{
		setter(x)
		{
			super.width = x;
			imageWidth = x;
			Butt_imageLoaded = false;
			update();
		}
		getter
		{
			return super.width;
		}
	}

	property height
	{
		setter(x)
		{
			super.height = x;
			imageHeight = x;
			Butt_imageLoaded = false;
			update();
		}
		getter
		{
			return super.height;
		}
	}

	function setSize(w, h)
	{
		super.setSize(w, h);
		setImageSize(w, h);
		Butt_imageLoaded = false;
		update();
	}

	property caption
	{
		setter(x)
		{
			Butt_caption = x;
			update();
		}
		getter
		{
			return Butt_caption;
		}
	}

	property color
	{
		setter(x)
		{
			Butt_color = int x;
			update();
		}
		getter
		{
			return Butt_color;
		}
	}

	property captionColor
	{
		setter(x)
		{
			Butt_captionColor = int x;
			update();
		}
		getter
		{
			return Butt_captionColor;
		}
	}

	property showFocusImage
	{
		setter(x) { Butt_showFocusImage = x; }
		getter { return Butt_showFocusImage; }
	}

	property onClickHandler
	{
		setter(x) { Butt_onClickHandler = x; }
		getter { return Butt_onClickHandler; }
	}
}


